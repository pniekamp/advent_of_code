import std.stdio;
import std.algorithm;
import std.flat_hash_map;
import std.scan;

fn main -> int
{
  try
  {
    var total = 0;
    var lines = std::vector<std::string>();

    var fin = std::file::open("day06.txt");

    for (var line : std::buffered_reader(&mut fin).lines)
    {
      lines.push_back(line);
    }

    for (var &op : lines.back.words)
    {
      var width = 1;
      var column = op.data - lines.back.data;

      for (var &line : lines.drop_back(1))
      {
        width = std::max(width, line.find_first(line.begin + column, ' ') - line.begin - column);
      }

      var adds = i64(0);
      var muls = i64(1);

      for (var i = column; i < column + width; ++i)
      {
        var value = i64(0);

        for (var &line : lines.drop_back(1))
          if (i < line.len && std::is_digit(line[i]))
            value = value * 10 + cast<i64>(line[i] - cast('0'));

        adds += value;
        muls *= value;
      }

      switch (op)
      {
        case "+":
          total += adds;

        case "*":
          total += muls;
      }
    }

    std::print("part2: grand total ", total);

  }
  catch(std::error e)
  {
    std::panic(e);
  }

  return 0;
}
