import std.stdio;
import std.algorithm;
import std.flat_hash_map;
import std.scan;

struct map
{
  std::vector<std::string> tiles;

  fn at(this &map, int x, int y) -> char
  {
    if (0 <= y && cast<usize>(y) < map.tiles.len && 0 <= x && cast<usize>(x) < map.tiles[cast<usize>(y)].len)
      return cast<char>(map.tiles[cast<usize>(y)][cast<usize>(x)]);

    return ' ';
  }

  map() = default;
  ~map() = default;
}

fn main -> int
{
  try
  {
    var count = 0;
    var map = map();

    var fin = std::file::open("day04.txt");

    for (var line : std::buffered_reader(&mut fin).lines)
    {
      map.tiles.push_back(line);
    }

    for (;;)
    {
      var removed = 0;

      for (var j = 0; j < cast<int>(map.tiles.len); ++j)
      {
        for (var i = 0; i < cast<int>(map.tiles[cast<usize>(j)].len); ++i)
        {
          if (map.at(i, j) != '@')
            continue;

          var neighbors = 0;

          for (var dp : [(+1, 0), (-1, 0), (0, +1), (0, -1), (+1, +1), (-1, +1), (+1, -1), (-1, -1)])
          {
            if (map.at(i + dp.0, j + dp.1) == '@')
              neighbors += 1;
          }

          if (neighbors < 4)
          {
            removed += 1;

            map.tiles[cast<usize>(j)][cast<usize>(i)] = cast('.');
          }
        }
      }

      if (removed == 0)
        break;

      count += removed;
    }

    std::print("part2: accesible rolls ", count);
  }
  catch(std::error e)
  {
    std::panic(e);
  }

  return 0;
}
