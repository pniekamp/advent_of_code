import std.stdio;
import std.algorithm;
import std.scan;

fn main -> int
{
  try
  {
    var sum = 0;
    var source = std::vector<i64>();

    var fin = std::file::open("day20.txt");

    for(var line : std::buffered_reader(&mut fin).lines)
    {
      var n = i64(0);
      if (!std::sscanf(line, "{}", &mut n))
        std::panic("error scaning number from input '", line, "'");

      source.push_back(n * 811589153);
    }

    var N = cast<i64>(source.len - 1);
    var mapping = std::vector::from(std::iota(0, source.len));

    for(var k = 0; k < 10; ++k)
    {
      for(var i = 0; i < source.len; ++i)
      {
        var j = mapping.erase(mapping.find(i)) - mapping.begin;
        mapping.insert(mapping.begin + cast<usize>((cast<i64>(j) + source[i]) % N + N) % mapping.len, i);

        //std::print(mapping.transform(|i| [source] source[i]));
      }
    }

    var j = mapping.find(source.find(0) - source.begin) - mapping.begin;

    for(var i : [1000, 2000, 3000])
      sum += source[mapping[(j + i) % mapping.len]];

    std::print("part2: sum of the grove coordinates ", sum);

  }
  catch(std::error e)
  {
    std::panic(e);
  }

  return 0;
}
