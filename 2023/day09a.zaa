import std.stdio;
import std.algorithm;
import std.scan;

struct sensor
{
  std::vector<std::vector<i64>> readings;

  sensor() = default;
  sensor(sensor &&) = default;
  ~sensor() = default;
}

fn main -> int
{
  try
  {
    var sensor = sensor();

    var fin = std::file::open("day09.txt");

    for (var line : std::buffered_reader(&mut fin).lines)
    {
      var readings = std::vector<i64>();

      while (!line.empty)
      {
        var reading = i64(0);
        line.remove_prefix!(std::sscanf(line, "{} ", &mut reading).len);

        readings.push_back(reading);
      }

      sensor.readings.push_back(readings);
    }

    var sum = i64(0);

    for (var readings : sensor.readings)
    {
      var prediction = readings.back;

      while (!readings.empty)
      {
        readings = std::vector::from(std::adjacent<2>(readings).transform(|pair| pair.1 - pair.0));

        if (std::all_of(readings, |reading| reading == 0))
          break;

        prediction += readings.back;
      }

      sum += prediction;
    }

    std::print("part1: prediction ", sum);

  }
  catch(std::error e)
  {
    std::panic(e);
  }

  return 0;
}
